pipeline GTFSPipeline {

    block GTFSHttpExtractor oftype HttpExtractor {
        url: "https://gtfs.rhoenenergie-bus.de/GTFS.zip";
    }

    block ZipArchiveInterpreter oftype ArchiveInterpreter {
        archiveType: "zip";
    }

    block StopsFilePicker oftype FilePicker {
        path: "/stops.txt";
    }

    block StopsTextFileInterpreter oftype TextFileInterpreter {
        encoding: "utf8";
    }

    block StopsCSVInterpreter oftype CSVInterpreter {
        enclosing: '"';
    }

    block StopsTableInterpreter oftype TableInterpreter {
        header: true;
        columns:[
            "stop_id" oftype integer,
            "stop_name" oftype text,
            "stop_lat" oftype coordinate,
            "stop_lon" oftype coordinate,
            "zone_id" oftype zone_id
        ];
    }

    valuetype zone_id oftype integer {
        constraints: [ zone_id_2001 ];
    }

    valuetype coordinate oftype decimal { 
        constraints: [ CoordinateScale ]; 
    }

    constraint zone_id_2001 on integer:
        value == 2001;

    constraint CoordinateScale oftype RangeConstraint { 
        lowerBound: -90; 
        lowerBoundInclusive: true; 
        upperBound: 90; 
        upperBoundInclusive: true;
    }

    block StopsLoader oftype SQLiteLoader {
        table: "stops";
        file: "./gtfs.sqlite";
    }

    GTFSHttpExtractor
        -> ZipArchiveInterpreter
        -> StopsFilePicker
        -> StopsTextFileInterpreter
        -> StopsCSVInterpreter
        -> StopsTableInterpreter
        -> StopsLoader;
}